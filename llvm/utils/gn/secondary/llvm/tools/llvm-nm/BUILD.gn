import("//llvm/tools/binutils_symlinks.gni")
import("//llvm/utils/gn/build/symlink_or_copy.gni")

if (llvm_install_binutils_symlinks) {
  symlink_or_copy("nm") {
    data_deps = [ ":llvm-nm" ]
    source = "llvm-nm"
    output = "$root_out_dir/bin/nm"
  }
}

# //:llvm-nm depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  data_deps = [ ":llvm-nm" ]
  if (llvm_install_binutils_symlinks) {
    data_deps += [ ":nm" ]
  }
}

executable("llvm-nm") {
  deps = [
    "//llvm/lib/Bitcode/Reader",
    "//llvm/lib/Demangle",
    "//llvm/lib/IR",
    "//llvm/lib/Object",
    "//llvm/lib/Support",
    "//llvm/lib/Target:AllTargetsAsmParsers",
    "//llvm/lib/Target:AllTargetsDescs",
    "//llvm/lib/Target:AllTargetsInfos",
  ]
  sources = [
    # Make `gn format` not collapse this, for sync_source_lists_from_cmake.py.
    "llvm-nm.cpp",
  ]
}
