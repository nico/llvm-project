static_library("Host") {
  output_name = "lldbHost"
  configs += [ "//llvm/utils/gn/build:lldb_code" ]
  deps = [
    "//llvm/lib/Object",
    "//llvm/lib/Support",

    "//lldb/source/Utility",

    # FIXME:
    # LINK_LIBS
    #   ${EXTRA_LIBS}
    #   ${LLDBObjCLibs}
    #   ${LLDB_LIBEDIT_LIBS}
  ]
  #include_dirs = [ ".." ]

  sources = [
    "common/FileAction.cpp",
    "common/FileCache.cpp",
    "common/File.cpp",
    "common/FileSystem.cpp",
    "common/GetOptInc.cpp",
    "common/Host.cpp",
    "common/HostInfoBase.cpp",
    "common/HostNativeThreadBase.cpp",
    "common/HostProcess.cpp",
    "common/HostThread.cpp",
    "common/LockFileBase.cpp",
    "common/LZMA.cpp",
    "common/MainLoop.cpp",
    "common/MonitoringProcessLauncher.cpp",
    "common/NativeProcessProtocol.cpp",
    "common/NativeRegisterContext.cpp",
    "common/NativeThreadProtocol.cpp",
    "common/NativeWatchpointList.cpp",
    "common/OptionParser.cpp",
    "common/PipeBase.cpp",
    "common/ProcessLaunchInfo.cpp",
    "common/ProcessRunLock.cpp",
    "common/PseudoTerminal.cpp",
    "common/SocketAddress.cpp",
    "common/Socket.cpp",
    "common/StringConvert.cpp",
    "common/TCPSocket.cpp",
    "common/Terminal.cpp",
    "common/ThreadLauncher.cpp",
    "common/UDPSocket.cpp",
    "common/XML.cpp",
  ]

  lldb_enable_libedit = false  # FIXME
  if (lldb_enable_libedit) {
    sources += [ "common/Editline.cpp" ]
  }

  # XXX why is this separate
  sources += [ "posix/ConnectionFileDescriptorPosix.cpp" ]

  if (current_os == "win") {
    sources += [
      "windows/ConnectionGenericFileWindows.cpp",
      "windows/FileSystem.cpp",
      "windows/Host.cpp",
      "windows/HostInfoWindows.cpp",
      "windows/HostProcessWindows.cpp",
      "windows/HostThreadWindows.cpp",
      "windows/LockFileWindows.cpp",
      "windows/PipeWindows.cpp",
      "windows/ProcessLauncherWindows.cpp",
      "windows/ProcessRunLock.cpp",
      "windows/Windows.cpp",
    ]
  } else {
    sources += [
      "posix/DomainSocket.cpp",
      "posix/FileSystemPosix.cpp",
      "posix/HostInfoPosix.cpp",
      "posix/HostProcessPosix.cpp",
      "posix/HostThreadPosix.cpp",
      "posix/LockFilePosix.cpp",
      "posix/PipePosix.cpp",
      "posix/ProcessLauncherPosixFork.cpp",
    ]
  }

  if (current_os == "mac") {
    sources += [
      "macosx/cfcpp/CFCBundle.cpp",
      "macosx/cfcpp/CFCData.cpp",
      "macosx/cfcpp/CFCMutableArray.cpp",
      "macosx/cfcpp/CFCMutableDictionary.cpp",
      "macosx/cfcpp/CFCMutableSet.cpp",
      "macosx/cfcpp/CFCString.cpp",
    ]
    deps += [ "macosx/objcxx" ]
    # FIXME: Maybe define NO_XPC_SERVICES
  }

  if (current_os == "linux" || current_os == "android") {
    sources += [
      "linux/AbstractSocket.cpp",
      "linux/Host.cpp",
      "linux/HostInfoLinux.cpp",
      "linux/LibcGlue.cpp",
      "linux/Support.cpp",
    ]
  }
  if (current_os == "android") {
    sources += [
      "android/HostInfoAndroid.cpp",
      "android/LibcGlue.cpp",
    ]
  }

  if (current_os == "freebsd") {
    sources += [
      "freebsd/Host.cpp",
      "freebsd/HostInfoFreeBSD.cpp",
    ]
  }
  if (current_os == "netbsd") {
    sources += [
      "netbsd/HostNetBSD.cpp",
      "netbsd/HostInfoNetBSD.cpp",
    ]
  }
  if (current_os == "openbsd") {
    sources += [
      "openbsd/Host.cpp",
      "openbsd/HostInfoOpenBSD.cpp",
    ]
  }
  # FIXME:
  # set(EXTRA_LIBS)
  # if (CMAKE_SYSTEM_NAME MATCHES "NetBSD")
  #   list(APPEND EXTRA_LIBS kvm)
  # endif()
  # if (LLDB_ENABLE_LIBXML2)
  #   list(APPEND EXTRA_LIBS ${LIBXML2_LIBRARIES})
  # endif()
  # if (HAVE_LIBDL)
  #   list(APPEND EXTRA_LIBS ${CMAKE_DL_LIBS})
  # endif()
  # if (LLDB_ENABLE_LIBEDIT)
  #   list(APPEND EXTRA_LIBS ${LibEdit_LIBRARIES})
  # endif()
  # if (LLDB_ENABLE_LZMA)
  #   list(APPEND EXTRA_LIBS ${LIBLZMA_LIBRARIES})
  # endif()
  # if (WIN32)
  #   list(APPEND LLDB_SYSTEM_LIBS psapi)
  # endif()
}
